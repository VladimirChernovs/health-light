plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id "idea"
}

idea {
	module {
		excludeDirs += files("node_modules")
	}
}

defaultTasks "bootRun"

springBoot {
	mainClassName = "com.black.mono.HealthApp"
}

test {
	useJUnitPlatform()
	exclude "**/*IT*", "**/*IntTest*"

	testLogging {
		events 'FAILED', 'SKIPPED'
	}
	reports.html.enabled = true
}

task integrationTest(type: Test) {
	useJUnitPlatform()
	description = "Execute integration tests."
	group = "verification"
	include "**/*IT*", "**/*IntTest*"

	testLogging {
		events 'FAILED', 'SKIPPED'
	}
	reports.html.enabled = true
}

group = 'com.black'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

ext {
	springfoxVersion = '3.0.0'
	mapstruct_version = '1.3.1.Final'
	archunit_junit5_version = '0.14.1'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation "org.hibernate.validator:hibernate-validator"
	implementation "io.springfox:springfox-boot-starter:${springfoxVersion}"

	compile "org.apache.commons:commons-lang3:3.11"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310" // using Java 8 date and time classes

	implementation 'org.projectlombok:lombok'
	implementation "org.mapstruct:mapstruct:${mapstruct_version}"
	annotationProcessor("org.projectlombok:lombok") //<<=== Lombok before mapstruct
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"

	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation ("org.springframework.boot:spring-boot-starter-test") {
		exclude group: "org.junit.vintage", module: "junit-vintage-engine"
	}
	testImplementation "org.springframework.security:spring-security-test"
	testImplementation "org.springframework.boot:spring-boot-test"
	testImplementation "com.tngtech.archunit:archunit-junit5-api:${archunit_junit5_version}"
	testRuntimeOnly "com.tngtech.archunit:archunit-junit5-engine:${archunit_junit5_version}"
	testImplementation "com.h2database:h2"
}
